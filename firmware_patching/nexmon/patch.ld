MEMORY
{
	rom : ORIGIN = 0x0, LENGTH = 640K
	ram : ORIGIN = 0x180000, LENGTH = 768K
	patch : ORIGIN = 0x180020, LENGTH = 0x7e0
	ucode : ORIGIN = 0x1FD82C, LENGTH = 0xb2e0 
}
/* 0x1FD820 on .40 firmware */

SECTIONS
{
	/* include the patch.generated.ld file that was generated by the nexmon gcc plugin after evaluating the "at" attributes */
	INCLUDE radiotap.generated.ld
	INCLUDE flashpatch.generated.ld
	INCLUDE patch.generated.ld
	INCLUDE wrapper.generated.ld

	/* All other sections that were not yet placed but referenced by a kept section are collected in the text section */
	.text.patch : { patch.o (.text .text.* .data .data.* .bss .bss.* .rodata .rodata.*) } >patch
	.text.patch : { radiotap.o (.text .text.* .data .data.* .bss .bss.* .rodata .rodata.*) } >patch

	/* write deflation functions and compressed ucode to address of original ucode */
	.text.ucode : { ucode_compressed.o (.text .text.* .data .data.* .bss .bss.* .rodata .rodata.*) } >ucode
	.text.ucode : { ucode_compression_code.o (.text .text.* .data .data.* .bss .bss.* .rodata .rodata.*) } >ucode
	.text.ucode : { flashpatch.o (.text .text.* .data .data.* .bss .bss.* .rodata .rodata.*) } >ucode
}
